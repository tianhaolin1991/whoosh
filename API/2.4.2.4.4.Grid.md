# Grid

网格容器，由“行”和“列”分割的单元格所组成，通过指定“项目”所在的单元格做出各种各样的布局。

## Component Constructors

### public init()

```cangjie
public init()
```
功能： 创建网格容器。

### public init(scroller: Scroller)

```cangjie
public init(scroller: Scroller)
```
功能： 创建包含滚动控制器的网格容器。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Scroller | 是 | 可滚动组件的控制器，与可滚动组件绑定。 说明： 不允许和其他滚动类组件，如： List 、 Grid 、 Scroll 等绑定同一个滚动控制对象。 | scroller | - |

### public init(child: () -> Unit)

```cangjie
public init(child: () -> Unit)
```
功能： 创建包含子组件的网格容器。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ()->Unit | 是 | 网格容器的子组件。 | child | - |

### public init(scroller: Scroller, child: () -> Unit)

```cangjie
public init(scroller: Scroller, child: () -> Unit)
```
功能： 创建包含滚动控制器和子组件的网格容器。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Scroller | 是 | 可滚动组件的控制器，与可滚动组件绑定。 说明： 不允许和其他滚动类组件，如： List 、 Grid 、 Scroll 等绑定同一个滚动控制对象。 | scroller | - |
| ()->Unit | 是 | 网格容器的子组件。 | child | - |


## Functions

### public func alignItems(alignment: GridItemAlignment): This

```cangjie
public func alignItems(alignment: GridItemAlignment): This
```
功能： 设置Grid中GridItem的对齐方式， 使用方法可以参考 示例4 。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| GridItemAlignment | 是 | 设置Grid中GridItem的对齐方式。 初始值：GridItemAlignment.DEFAULT | alignment | - |

### public func cachedCount(cacheCount: Int32): This

```cangjie
public func cachedCount(cacheCount: Int32): This
```
功能： 设置预加载的GridItem的数量，只在 LazyForEach 中生效。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 预加载的GridItem的数量。 初始值：垂直滚动时为一个屏幕内可显示的行数，水平滚动时为一个屏幕内可显示的列数，最大值为16。 取值范围：[0, +∞)，设置为小于0的值时，按1处理。 | cacheCount | - |

### public func cachedCount(cacheCount: Int32, show: Bool): This

```cangjie
public func cachedCount(cacheCount: Int32, show: Bool): This
```
功能： 设置预加载的GridItem数量，并配置是否显示预加载节点。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 预加载的GridItem的数量。 初始值：垂直滚动时为一个屏幕内可显示的行数，水平滚动时为一个屏幕内可显示的列数，最大值为16。 取值范围：[0, +∞)，设置为小于0的值时，按1处理。 | cacheCount | - |
| Bool | 是 | 被预加载的GridItem是否需要显示。 初始值：false，不显示预加载的GridItem。 | show | - |

### public func cellLength(value: Int32): This

```cangjie
public func cellLength(value: Int32): This
```
功能： 设置一行的高度或者一列的宽度。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 一行的高度或者一列的宽度。 初始值：第一个元素的大小 单位：vp 取值范围：[0, +∞)，设置为小于0的值时，按初始值显示。 | value | - |

### public func columnsGap(value: Length): This

```cangjie
public func columnsGap(value: Length): This
```
功能： 设置列与列的间距。设置为小于0的值时，按初始值显示。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 列与列的间距。 初始值：0 取值范围：[0, +∞) | value | - |

### public func columnsTemplate(value: String): This

```cangjie
public func columnsTemplate(value: String): This
```
功能： 设置当前网格布局列的数量、固定列宽或最小列宽值，不设置时默认1列。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| String | 是 | 当前网格布局列的数量或最小列宽值。 | value | - |

### public func edgeEffect(value: EdgeEffect): This

```cangjie
public func edgeEffect(value: EdgeEffect): This
```
功能： 设置边缘滑动效果。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| EdgeEffect | 是 | Grid组件的边缘滑动效果，支持弹簧效果和阴影效果。 初始值：EdgeEffect.None | value | - |

### public func edgeEffect(value: EdgeEffect, options: EdgeEffectOptions): This

```cangjie
public func edgeEffect(value: EdgeEffect, options: EdgeEffectOptions): This
```
功能： 设置边缘滑动效果。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| EdgeEffect | 是 | Grid组件的边缘滑动效果，支持弹簧效果和阴影效果。 初始值：EdgeEffect.None | value | - |
| EdgeEffectOptions | 是 | 组件内容大小小于组件自身时，是否开启滑动效果。设置为{ alwaysEnabled: true }会开启滑动效果，{ alwaysEnabled: false }不开启。 初始值：{ alwaysEnabled: false } | options | - |

### public func editMode(isEditMode: Bool): This

```cangjie
public func editMode(isEditMode: Bool): This
```
功能： 设置Grid是否进入编辑模式，进入编辑模式可以拖拽Grid组件内部 GridItem 。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | Grid是否进入编辑模式。 初始值：false，当前Grid组件不处于可编辑模式。 | isEditMode | - |

### public func enableScrollInteraction(isEnable: Bool): This

```cangjie
public func enableScrollInteraction(isEnable: Bool): This
```
功能： 设置是否支持滚动手势。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 是否支持滚动手势，当设置为false时，无法通过手指或者鼠标滚动，但不影响控制器的滚动接口。 初始值：true | isEnable | - |

### public func friction(value: Float64): This

```cangjie
public func friction(value: Float64): This
```
功能： 设置摩擦系数，手动划动滚动区域时生效，仅影响惯性滚动过程，对惯性滚动过程中的链式效果有间接影响。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float64 | 是 | 摩擦系数。 初始值：非可穿戴设备为0.75，可穿戴设备为0.9。 取值范围：(0, +∞)，设置为小于等于0的值时，按初始值处理。 | value | - |

### public func friction(value: Int32): This

```cangjie
public func friction(value: Int32): This
```
功能： 设置摩擦系数，手动划动滚动区域时生效，仅影响惯性滚动过程，对惯性滚动过程中的链式效果有间接影响。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 摩擦系数。 初始值：未调用该属性方法时，初始值参考 friction 。 取值范围：(0, +∞)，设置为小于等于0的值时，按初始值处理。 | value | - |

### public func layoutDirection(value: GridDirection): This

```cangjie
public func layoutDirection(value: GridDirection): This
```
功能： 设置布局的主轴方向。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| GridDirection | 是 | 布局的主轴方向。 初始值：GridDirection.Row | value | - |

### public func maxCount(value: Int32): This

```cangjie
public func maxCount(value: Int32): This
```
功能： 设置可显示的最大行数或列数。设置为小于1的值时，按初始值显示。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 可显示的最大行数或列数。 | value | - |

### public func minCount(value: Int32): This

```cangjie
public func minCount(value: Int32): This
```
功能： 设置可显示的最小行数或列数。设置为小于1的值时，按初始值显示。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 可显示的最小行数或列数。 初始值：1 | value | - |

### public func multiSelectable(isSelectable: Bool): This

```cangjie
public func multiSelectable(isSelectable: Bool): This
```
功能： 设置是否开启鼠标框选。开启框选后，可以配合Griditem的 selected 属性和 onSelect 事件获取GridItem的选中状态，还可以设置 选中态样式 （无默认选中样式）。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 是否开启鼠标框选。 初始值：false false：关闭框选。true：开启框选。 | isSelectable | - |

### public func nestedScroll(value: NestedScrollOptions): This

```cangjie
public func nestedScroll(value: NestedScrollOptions): This
```
功能： 设置嵌套滚动选项。设置向前和向后两个方向上的嵌套滚动模式，实现与父组件的滚动联动。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| NestedScrollOptions | 是 | 嵌套滚动选项。 | value | - |

### public func rowsGap(value: Length): This

```cangjie
public func rowsGap(value: Length): This
```
功能： 设置行与行的间距。设置为小于0的值时，按初始值显示。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 用于设置行与行的间距。 初始值：0 取值范围：[0, +∞) | value | - |

### public func rowsTemplate(value: String): This

```cangjie
public func rowsTemplate(value: String): This
```
功能： 设置当前网格布局行的数量、固定行高或最小行高值，不设置时默认1行。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| String | 是 | 当前网格布局行的数量或最小行高值。 | value | - |

### public func scrollBar(value: BarState): This

```cangjie
public func scrollBar(value: BarState): This
```
功能： 设置滚动条状态。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| BarState | 是 | 滚动条状态。 初始值：BarState.Auto | value | - |

### public func scrollBarColor(color: ResourceColor): This

```cangjie
public func scrollBarColor(color: ResourceColor): This
```
功能： 设置滚动条的颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 滚动条的颜色。 初始值：0x182431（40%不透明度） 为HEX格式颜色，支持rgb或者argb，示例：0xffffff。 | color | - |

### public func scrollBarWidth(value: Length): This

```cangjie
public func scrollBarWidth(value: Length): This
```
功能： 设置滚动条的宽度，不支持百分比设置。宽度设置后，滚动条正常状态和按压状态宽度均为滚动条的宽度值。如果滚动条的宽度超过Grid组件主轴方向的高度，则滚动条的宽度会变为初始值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 滚动条的宽度。 初始值：4 单位：vp 取值范围：设置为小于0的值时，按初始值处理。设置为0时，不显示滚动条。 | value | - |

### public func supportAnimation(isSupportAnimation: Bool): This

```cangjie
public func supportAnimation(isSupportAnimation: Bool): This
```
功能： 设置是否支持动画。当前支持GridItem拖拽动画。仅在滚动模式下（只设置rowsTemplate、columnsTemplate其中一个）支持动画。仅在大小规则的Grid中支持拖拽动画，跨行或跨列场景不支持。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 是否支持动画。 初始值：false，不支持动画。 | isSupportAnimation | - |

### public func onItemDragEnter(callback: (ItemDragInfo) -> Unit): This

```cangjie
public func onItemDragEnter(callback: (ItemDragInfo) -> Unit): This
```
功能： 拖拽进入网格元素范围内时触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo )->Unit | 是 | 拖拽进入网格元素范围内时触发。 参数一：拖拽点的信息。 | callback | - |

### public func onItemDragLeave(callback: (ItemDragInfo, Int32) -> Unit): This

```cangjie
public func onItemDragLeave(callback: (ItemDragInfo, Int32) -> Unit): This
```
功能： 拖拽离开网格元素时触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo ,Int32)->Unit | 是 | 拖拽离开网格元素时触发。 参数一：拖拽点的信息。 参数二：拖拽离开的网格元素索引值。 | callback | - |

### public func onItemDragMove(callback: (ItemDragInfo, Int32, Int32) -> Unit): This

```cangjie
public func onItemDragMove(callback: (ItemDragInfo, Int32, Int32) -> Unit): This
```
功能： 拖拽在网格元素范围内移动时触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo ,Int32,Int32)->Unit | 是 | 拖拽在网格元素范围内移动时触发。 参数一：拖拽点的信息。 参数二：拖拽起始位置。 参数三：拖拽插入位置。 | callback | - |

### public func onItemDragStart(callback: (ItemDragInfo, Int32) -> Unit): This

```cangjie
public func onItemDragStart(callback: (ItemDragInfo, Int32) -> Unit): This
```
功能： 开始拖拽网格元素时触发。返回Unit表示不能拖拽。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo ,Int32)->Unit | 是 | 开始拖拽网格元素时触发。 参数一：拖拽点的信息。 参数二：被拖拽网格元素索引值。 | callback | - |

### public func onItemDragStart(callback: (ItemDragInfo, Int32) -> (() -> Unit)): This

```cangjie
public func onItemDragStart(callback: (ItemDragInfo, Int32) -> (() -> Unit)): This
```
功能： 开始拖拽网格元素时触发。返回() -> Unit表示能拖拽。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo ,Int32)->(()->Unit) | 是 | 开始拖拽网格元素时触发。 参数一：拖拽点的信息。 参数二：被拖拽网格元素索引值。 | callback | - |

### public func onItemDrop(callback: (ItemDragInfo, Int32, Int32, Bool) -> Unit): This

```cangjie
public func onItemDrop(callback: (ItemDragInfo, Int32, Int32, Bool) -> Unit): This
```
功能： 绑定该事件的网格元素可作为拖拽释放目标，当GridItem停止拖拽时触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( ItemDragInfo ,Int32,Int32,Bool)->Unit | 是 | 绑定该事件的网格元素可作为拖拽释放目标，当GridItem停止拖拽时触发。 参数一：拖拽点的信息。 参数二：拖拽起始位置。 参数三：拖拽插入位置。 参数四：是否成功释放。当拖拽释放位置在网格元素之内时，会返回true；在网格元素之外时，会返回false。 | callback | - |

### public func onReachEnd(callback: () -> Unit): This

```cangjie
public func onReachEnd(callback: () -> Unit): This
```
功能： 网格到达末尾位置时触发。

### public func onReachStart(callback: () -> Unit): This

```cangjie
public func onReachStart(callback: () -> Unit): This
```
功能： 网格到达起始位置时触发。

### public func onScrollBarUpdate(callback: (Int32, Float64) -> ComputedBarAttribute): This

```cangjie
public func onScrollBarUpdate(callback: (Int32, Float64) -> ComputedBarAttribute): This
```
功能： 在Grid每帧布局结束时触发，可通过该回调设置滚动条的位置及长度。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (Int32,Float64)-> ComputedBarAttribute | 是 | 在Grid每帧布局结束时触发，可通过该回调设置滚动条的位置及长度。 参数一：当前显示的网格起始位置的索引值。 参数二：当前显示的网格起始位置元素相对网格显示起始位置的偏移，单位vp。 | callback | - |

**Returns:**

| type | description |
| --- | --- |
| ComputedBarAttribute | 滚动条的位置及长度。 |

### public func onScrollFrameBegin(callback: (Float64, ScrollState) -> OffsetOption): This

```cangjie
public func onScrollFrameBegin(callback: (Float64, ScrollState) -> OffsetOption): This
```
功能： 网格开始滑动时触发，事件参数传入即将发生的滑动量，事件处理函数中可根据应用场景计算实际需要的滑动量并作为事件处理函数的返回值返回，网格将按照返回值的实际滑动量进行滑动。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (Float64, ScrollState )-> OffsetOption | 是 | 网格开始滑动时触发。 参数一：即将发生的滑动量，单位vp。 参数二：当前滑动状态。 | callback | - |

**Returns:**

| type | description |
| --- | --- |
| OffsetOption | 实际滑动量，单位vp。 |

### public func onScrollIndex(callback: (UInt32)-> Unit): This

```cangjie
public func onScrollIndex(callback: (UInt32)-> Unit): This
```
功能： 当前网格显示的起始位置/终止位置的item发生变化时触发。网格初始化时会触发一次。Grid显示区域上第一个子组件/最后一个组件的索引值有变化就会触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (UInt32)->Unit | 是 | 当前列表显示的起始位置 item 发生变化时触发。 | callback | - |

### public func onScrollIndex(callback: (UInt32, UInt32)-> Unit): This

```cangjie
public func onScrollIndex(callback: (UInt32, UInt32)-> Unit): This
```
功能： 当前网格显示的起始位置/终止位置的item发生变化时触发。网格初始化时会触发一次。Grid显示区域上第一个子组件/最后一个组件的索引值有变化就会触发。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (UInt32,UInt32)->Unit | 是 | 当前列表显示的起始位置/终止位置 item 发生变化时触发。 参数一：当前显示的网格起始位置的索引值。 参数二：当前显示的网格终止位置的索引值。 | callback | - |

### public func onScrollStart(callback: () -> Unit): This

```cangjie
public func onScrollStart(callback: () -> Unit): This
```
功能： 网格滑动开始时触发。手指拖动网格或网格的滚动条触发的滑动开始时，会触发该事件。使用 Scroller 滑动控制器触发的带动画的滑动，动画开始时会触发该事件。

### public func onScrollStop(callback: () -> Unit): This

```cangjie
public func onScrollStop(callback: () -> Unit): This
```
功能： 网格滑动停止时触发。手指拖动网格或网格的滚动条触发的滑动，手指离开屏幕并且滑动停止时会触发该事件。使用 Scroller 滑动控制器触发的带动画的滑动，动画停止会触发该事件。


## Classes

### GridLayoutOptions

class GridLayoutOptions
```cangjie
public class GridLayoutOptions { public init ( regularSize: (Int32, Int32), irregularIndexes!: Option<Array<Int32>> = Option.None, onGetIrregularSizeByIndex!: Option<(Int32) -> (Int32, Int32)> = Option.None, onGetRectByIndex!: Option<(Int32) -> (Int32, Int32, Int32, Int32)> = Option.None ) }
```
功能： Grid布局选项。其中，irregularIndexes和onGetIrregularSizeByIndex可对仅设置rowsTemplate或columnsTemplate的Grid使用，可以指定一个index数组，并为其中的index对应的GridItem设置其占据的行数与列数；onGetRectByIndex可对同时设置rowsTemplate和columnsTemplate的Grid使用，为指定的index对应的GridItem设置位置和大小。



## Structs

### ComputedBarAttribute

struct ComputedBarAttribute
```cangjie
public struct ComputedBarAttribute { public ComputedBarAttribute ( public let totalOffset: Float64, public let totalLength: Float64 ) }
```
功能： 滚动条位置和长度对象。


### OffsetOption

struct OffsetOption
```cangjie
public struct OffsetOption { public OffsetOption ( public let offsetRemain: Float64 ) }
```
功能： 实际滑动量。



## Enums

### GridDirection

enum GridDirection
```cangjie
public enum GridDirection { | Row | Column | RowReverse | ColumnReverse }
```
功能： 主轴布局方向枚举。

#### Column

```cangjie
Column
```
功能： 主轴布局方向沿垂直方向布局，即自上往下先填满一列，再去填下一列。

#### ColumnReverse

```cangjie
ColumnReverse
```
功能： 主轴布局方向沿垂直方向反向布局，即自下往上先填满一列，再去填下一列。

#### Row

```cangjie
Row
```
功能： 主轴布局方向沿水平方向布局，即自左往右先填满一行，再去填下一行。

#### RowReverse

```cangjie
RowReverse
```
功能： 主轴布局方向沿水平方向反向布局，即自右往左先填满一行，再去填下一行。


### GridItemAlignment

enum GridItemAlignment
```cangjie
public enum GridItemAlignment { | DEFAULT | STRETCH }
```
功能： GridItem的对齐方式枚举。

#### DEFAULT

```cangjie
DEFAULT
```
功能： 使用Grid的默认对齐方式。

#### STRETCH

```cangjie
STRETCH
```
功能： 以一行中的最高的GridItem作为其他GridItem的高度。



## Examples

```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}

@Entry
@Component
class EntryView {
 @State
 var numbers: Array<String> = ['0', '1', '2', '3', '4']
 var scroller: Scroller = Scroller()
 @State
 var gridPosition: Int64 = 0 //0代表滚动到grid顶部，1代表中间值，2代表滚动到grid底部。

 func build() {
 Column(5) {
 Text("scroll").fontColor(0xCCCCCC).fontSize(9).width(90.percent)
 Grid(this.scroller) {
 ForEach(
 this.numbers,
 itemGeneratorFunc: {
 day: String, idx: Int64 => ForEach(
 this.numbers,
 itemGeneratorFunc: {
 day: String, idx: Int64 => GridItem() {
 Text(day)
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(100.percent)
 .height(80)
 .textAlign(TextAlign.Center)
 }
 }
 )
 }
 )
 }
 .columnsTemplate("1fr 1fr 1fr 1fr 1fr")
 .columnsGap(10)
 .rowsGap(10)
 .friction(0.6)
 .enableScrollInteraction(true)
 .supportAnimation(false)
 .multiSelectable(false)
 .edgeEffect(EdgeEffect.Spring)
 .scrollBar(BarState.On)
 .scrollBarColor(Color.GRAY)
 .scrollBarWidth(4)
 .width(90.percent)
 .backgroundColor(0xFAEEE0)
 .height(300)
 .onScrollIndex(
 {
 first: UInt32, last: UInt32 =>
 AppLog.info(first.toString())
 AppLog.info(last.toString())
 }
 )
 .onScrollBarUpdate(
 {
 index: Int32, offset: Float64 =>
 AppLog.info(
 "XXX" + 'Grid onScrollBarUpdate,index : ' + index.toString() + ",offset" + offset.toString()
 )
 return ComputedBarAttribute(Float64((index / 5) * (80 + 10)) - offset, Float64(80 * 5 + 10 * 4))
 }
 )
 .onScrollStart({
 => AppLog.info("XXX" + "Grid onScrollStart")
 })
 .onScrollStop({
 => AppLog.info("XXX" + "Grid onScrollStop")
 })
 .onReachStart(
 {
 =>
 this.gridPosition = 0
 AppLog.info("XXX" + "Grid onReachStart")
 }
 )
 .onReachEnd(
 {
 =>
 this.gridPosition = 2
 AppLog.info("XXX" + "Grid onReachEnd")
 }
 )

 Button('next page').onClick({
 => // 点击后滑到下一页
 this.scroller.scrollPage(true)
 })
 }.width(100.percent).margin(top: 5)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}

@Entry
@Component
class EntryView {
 @State
 var colors: Array<UInt32> = [0xFFC0CB, 0xDA70D6, 0x6B8E23, 0x6A5ACD, 0x00FFFF, 0x00FF7F]
 @State
 var numbers: ArrayList<String> = ArrayList<String>()
 @State
 var translateY: Int64 = 0
 var scroller: Scroller = Scroller()
 var gridScroller: Scroller = Scroller()
 var touchDown: Bool = false
 var listTouchDown: Bool = false
 var scrolling: Bool = false

 protected override func aboutToAppear() {
 for (i in 1..100) {
 this.numbers.add("${i}")
 }
 }

 func build() {
 Stack() {
 Column() {
 Row() {
 Text("Head")
 }

 Column() {
 List(scroller: this.scroller) {
 ListItem() {
 Grid() {
 GridItem() {
 Text("GoodsTypeList1")
 }.backgroundColor(this.colors[0]).columnStart(0).columnEnd(1)

 GridItem() {
 Text("GoodsTypeList2")
 }.backgroundColor(this.colors[1]).columnStart(0).columnEnd(1)

 GridItem() {
 Text("GoodsTypeList3")
 }.backgroundColor(this.colors[2]).columnStart(0).columnEnd(1)

 GridItem() {
 Text("GoodsTypeList4")
 }.backgroundColor(this.colors[3]).columnStart(0).columnEnd(1)

 GridItem() {
 Text("GoodsTypeList5")
 }.backgroundColor(this.colors[4]).columnStart(0).columnEnd(1)
 }
 .scrollBar(BarState.Off)
 .columnsGap(15)
 .rowsGap(10)
 .rowsTemplate("1fr 1fr 1fr 1fr 1fr")
 .columnsTemplate("1fr")
 .width(100.percent)
 .height(200)
 }

 ListItem() {
 Grid(this.gridScroller) {
 ForEach(
 this.numbers,
 itemGeneratorFunc: {
 item: String, idx: Int64 => GridItem() {
 Text(item + "")
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(100.percent)
 .height(100.percent)
 .textAlign(TextAlign.Center)
 }
 .width(100.percent)
 .height(40)
 .shadow(radius: 10, color: Color(0x909399), offsetX: 1, offsetY: 1)
 .borderRadius(10)
 .translate(x: 0, y: this.translateY)
 }
 )
 }
 .columnsTemplate("1fr 1fr")
 .friction(0.3)
 .columnsGap(15)
 .rowsGap(10)
 .scrollBar(BarState.Off)
 .width(100.percent)
 .height(100.percent)
 .layoutDirection(GridDirection.Column)
 .nestedScroll(
 NestedScrollOptions(NestedScrollMode.PARENT_FIRST, NestedScrollMode.SELF_FIRST))
 .onTouch(
 {
 event => if (event.eventType.getValue() == TouchType.Down.getValue()) {
 this.listTouchDown = true
 } else if (event.eventType.getValue() == TouchType.Up.getValue()) {
 this.listTouchDown = false
 }
 })
 }
 }
 .scrollBar(BarState.Off)
 .edgeEffect(EdgeEffect.None)
 .onTouch(
 {
 event => if (event.eventType.getValue() == TouchType.Down.getValue()) {
 this.touchDown = true
 } else if (event.eventType.getValue() == TouchType.Up.getValue()) {
 this.touchDown = false
 }
 })
 .onScrollFrameBegin(
 {
 offset: Float64, state: ScrollState =>
 if (this.scrolling && offset > 0.0) {
 let newOffset = this.scroller.currentOffset().yOffset
 if (newOffset >= 590.0) {
 this.gridScroller.scrollBy(xOffset: 0.0, yOffset: offset)
 return 0.0
 } else if (newOffset + offset > 590.0) {
 this.gridScroller.scrollBy(xOffset: 0.0, yOffset: newOffset + offset - 590.0)
 return 590.0 - newOffset
 }
 }
 return offset
 }
 )
 .onScrollStart({
 => if (this.touchDown && !this.listTouchDown) {
 this.scrolling = true
 }
 })
 .onScrollStop({
 => this.scrolling = false
 })
 }.width(100.percent).height(100.percent).padding(left: 10, right: 10)
 }

 Row() {
 Text("Top").width(30).height(30).borderRadius(50)
 }
 .padding(5)
 .borderRadius(50)
 .backgroundColor(0xffffff)
 .shadow(radius: 10, color: Color(0x909399), offsetX: 1, offsetY: 1)
 .margin(right: 22, bottom: 15)
 .onClick(
 {
 event =>
 this.scroller.scrollTo(xOffset: 0, yOffset: 0)
 this.gridScroller.scrollTo(xOffset: 0, yOffset: 0)
 }
 )
 }.align(Alignment.BottomEnd)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import std.collection.*
import ohos.state_macro_manage.*

@Entry
@Component
class grid5{
 @State var numbers: ObservedArrayList<String> = ObservedArrayList<String>()
 var scroller: Scroller = Scroller()
 @State var text: String = 'drag'

 protected override func aboutToAppear(){
 for(i in 1..=15 : 1){
 numbers.append("${i}")
 }
 }

 @Builder func pixelMapBuilder(){
 Column(){
 Text(this.text)
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(80)
 .height(80)
 .textAlign(TextAlign.Center)
 }
 }

 func changeIndex(index1: Int64, index2: Int64){
 let temp: String
 temp = this.numbers[index1]
 this.numbers[index1] = this.numbers[index2]
 this.numbers[index2] = temp
 }

 func build(){
 Column(5){
 Grid(this.scroller){
 ForEach(this.numbers, {day: String, _: Int64 =>
 GridItem(){
 Text(day)
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(80)
 .height(80)
 .textAlign(TextAlign.Center)
 }
 })
 }
 .columnsTemplate('1fr 1fr 1fr')
 .columnsGap(10)
 .rowsGap(10)
 .width(90.percent)
 .backgroundColor(0xFAEEE0)
 .height(300)
 .editMode(true)
 .onItemDragStart({event: ItemDragInfo, itemIndex: Int32 =>
 this.text = this.numbers[Int64(itemIndex)]
 bind(this.pixelMapBuilder, this)()
 })
 .onItemDrop({event: ItemDragInfo, itemIndex: Int32, insertIndex: Int32, isSuccess: Bool =>
 if(!isSuccess || Int64(insertIndex) >= this.numbers.size){
 return
 }
 this.changeIndex(Int64(itemIndex), Int64(insertIndex))
 })
 }.width(100.percent).margin(top: 5)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}

@Entry
@Component
class EntryView {
 @State
 var numbers: ArrayList<String> = ArrayList<String>()

 protected override func aboutToAppear() {
 for (i in 1..31) {
 this.numbers.add("${i}")
 }
 }

 func build() {
 Scroll() {
 Column(5) {
 Blank()
 Text("rowsTemplate、columnsTemplate都不设置layoutDirection、maxcount、minCount、cellLength才生效")
 .fontSize(15)
 .fontColor(0xCCCCCC)
 .width(90.percent)
 Grid() {
 ForEach(
 this.numbers,
 itemGeneratorFunc: {
 day: String, idx: Int64 => GridItem() {
 Text(day).fontSize(16).backgroundColor(0xF9CF93)
 }.width(40).height(80).borderWidth(2).borderColor(Color.RED)
 }
 )
 }
 .height(300)
 .columnsGap(10)
 .rowsGap(10)
 .backgroundColor(0xFAEEE0)
 .maxCount(6)
 .minCount(2)
 .cellLength(0)
 .layoutDirection(GridDirection.Row)
 }.width(90.percent).margin(top: 5, left: 5, right: 5).align(Alignment.Center)
 }
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}

@Entry
@Component
class EntryView {
 @State
 var numbers: Array<String> = ['0', '1', '2', '3', '4', '5', '6', '7', '8', '9', '10', '11', '12', '13', '14', '15',
 '16', '17', '18', '19']
 @State
 var columns: Int64 = 2

 protected override func aboutToAppear() {
 let lastCount = AppStorage.get<Int64>("columnsCount")
 if (let Some(v) <- lastCount) {
 this.columns = lastCount.getOrThrow()
 }
 }

 func build() {
 Column(5) {
 Row() {
 Text("双指缩放改变列数").height(5.percent).margin(top: 10, left: 20)
 }

 Grid() {
 ForEach(
 this.numbers,
 itemGeneratorFunc: {
 day: String, idx: Int64 => ForEach(
 this.numbers,
 itemGeneratorFunc: {
 day: String, idx: Int64 => GridItem() {
 Text(day)
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(100.percent)
 .height(80)
 .textAlign(TextAlign.Center)
 }
 }
 )
 }
 )
 }
 .columnsTemplate(String.fromUtf8("1fr ".toArray().repeat(this.columns)))
 .columnsGap(10)
 .rowsGap(10)
 .width(90.percent)
 .scrollBar(BarState.Off)
 .backgroundColor(0xFAEEE0)
 .height(360.vp)
 .cachedCount(3)
 .transition(TransitionEffect.OPACITY.animation(AnimateParam(duration: 300, curve: Curve.Smooth)))
 .priorityGesture(
 PinchGesture().onActionEnd(
 {
 event: GestureEvent =>
 AppLog.info("end scale: ${event.scale}")
 // 手指分开，减少列数以放大Item，触发阈值可以自定义，示例为2
 if (event.scale > 2.0) {
 this.columns--
 } else if (event.scale < 0.6) {
 this.columns++
 }
 // 可以根据设备屏幕宽度设定最大和最小列数，此处以最小1列最大4列为例
 this.columns = min(4, max(1, this.columns))
 AppStorage.setOrCreate<Int64>("columnsCount", this.columns)
 }
 )
 )
 }.width(100.percent).margin(top: 5)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}

@Entry
@Component
class EntryView {
 var data: Array<Int64> = [0, 1, 2, 3, 4, 5]
 var data1: Array<Int64> = [0, 1, 2, 3, 4, 5]
 var data2: Array<Int64> = [0, 1, 2, 3, 4, 5]

 func build() {
 Column(10) {
 Text("auto-fill 根据设定的列宽自动计算列数").width(90.percent)
 Grid() {
 ForEach(
 this.data,
 itemGeneratorFunc: {
 item: Int64, idx: Int64 => GridItem() {
 Text("N ${item}").height(80)
 }.backgroundColor(Color.ORANGE)
 }
 )
 }
 .width(90.percent)
 .border(width: 1.vp, color: Color.BLACK)
 .columnsTemplate("repeat(auto-fill, 70)")
 .columnsGap(10)
 .rowsGap(10)
 .height(150)

 Text("auto-fit 先根据设定的列宽计算列数，余下的空间会均分到每一列中").width(90.percent)
 Grid() {
 ForEach(
 this.data1,
 itemGeneratorFunc: {
 item: Int64, idx: Int64 => GridItem() {
 Text("N ${item}").height(80)
 }.backgroundColor(Color.ORANGE)
 }
 )
 }
 .width(90.percent)
 .border(width: 1.vp, color: Color.BLACK)
 .columnsTemplate("repeat(auto-fit, 70)")
 .columnsGap(10)
 .rowsGap(10)
 .height(150)

 Text("auto-stretch 先根据设定的列宽计算列数，余下的空间会均分到每个列间距中").width(90.percent)
 Grid() {
 ForEach(
 this.data2,
 itemGeneratorFunc: {
 item: Int64, idx: Int64 => GridItem() {
 Text("N ${item}").height(80)
 }.backgroundColor(Color.ORANGE)
 }
 )
 }
 .width(90.percent)
 .border(width: 1.vp, color: Color.BLACK)
 .columnsTemplate('repeat(auto-stretch, 70)')
 .columnsGap(10)
 .rowsGap(10)
 .height(150)
 }.width(100.percent).height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import ohos.state_macro_manage.Entry
import ohos.state_macro_manage.Component
import ohos.state_macro_manage.State
import ohos.state_macro_manage.r
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import std.collection.{ArrayList, HashMap}
import std.math.*
import std.random.*

@Entry
@Component
class EntryView {
 @State
 var data: ArrayList<Int64> = ArrayList<Int64>()
 @State
 var items: ArrayList<Int64> = ArrayList<Int64>()

 protected override func aboutToAppear() {
 for (i in 0..100) {
 this.data.add(i)
 this.items.add(this.getSize())
 }
 }

 func getSize(): Int64 {
 let ret = Int64(floor(Random().nextFloat64() * 5.0))
 return max(1, ret)
 }

 func build() {
 Column(10) {
 Text('Grid alignItems示例代码')

 Grid() {
 ForEach(
 this.data,
 itemGeneratorFunc: {
 item: Int64, idx: Int64 =>
 // GridItem和Column不设置高度，默认会自适应子组件大小，设置STRETCH的场景下，会变成与当前行最高节点同高。
 // 若设置高度，则会保持已设置的高度，不会与当前行最高节点同高。
 GridItem() {
 Column() {
 Column().height(100).backgroundColor(0xD5D5D5).width(100.percent)
 // 中间的Text设置flexGrow(1)来自适应填满父组件的空缺
 Text(String.fromUtf8("这是一段文字。".toArray().repeat(this.items[item])))
 .flexGrow(1)
 .width(100.percent)
 .align(Alignment.TopStart)
 .backgroundColor(0xF7F7F7)
 Column().height(50).backgroundColor(0x707070).width(100.percent)
 }
 }.border(color: Color.BLACK, width: 1.vp)
 }
 )
 }
 .columnsGap(10)
 .rowsGap(5)
 .columnsTemplate("1fr 1fr")
 .width(80.percent)
 .height(100.percent)
 // Grid设置alignItems为STRETCH，以当前行最高的GridItem的高度为其他GridItem的高度。
 .alignItems(GridItemAlignment.STRETCH)
 .scrollBar(BarState.Off)
 }.height(100.percent).width(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import std.collection.*
import ohos.state_macro_manage.*

@Entry
@Component
class EntryView{
 @State var numbers: Array<String> = ['0', '1', '2', '3', '4']
 @State var rowNumbers: Array<String> = ['0', '1', '2', '3', '4', '5', '6', '7', '8', '9', '10']
 var scroller: Scroller = Scroller()

 func build(){
 Column(5){
 Text('scroll').fontColor(0xCCCCCC).fontSize(9).width(90.percent)
 Grid(this.scroller){
 ForEach(this.rowNumbers, {day: String, _: Int64 =>
 ForEach(this.numbers, {day: String, _: Int64 =>
 GridItem(){
 Text(day)
 .fontSize(16)
 .backgroundColor(0xF9CF93)
 .width(100.percent)
 .height(80)
 .textAlign(TextAlign.Center)
 }
 })
 })
 }
 .columnsTemplate('1fr 1fr 1fr 1fr 1fr')
 .columnsGap(10)
 .rowsGap(20)
 .height(90.percent)
 .fadingEdge(true, FadingEdgeOptions(fadingEdgeLength: 80.vp))
 }
 }
}
```

