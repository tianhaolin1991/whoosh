# Refresh

可以进行页面下拉操作并显示刷新动效的容器组件。

## Component Constructors

### public init(refreshparams: RefreshParams, content: () -> Unit)

```cangjie
public init(refreshparams: RefreshParams, content: () -> Unit)
```
功能： 创建refresh组件。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| RefreshParams | 是 | 设置组件刷新时的参数。 | refreshparams | - |
| ()->Unit | 是 | 声明容器子组件。 | content | - |


## Functions

### public func pullDownRatio(value: Option<Float64>): This

```cangjie
public func pullDownRatio(value: Option<Float64>): This
```
功能： 设置下拉跟手系数。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Option<Float64> | 是 | 下拉跟手系数。数值越大，跟随手势下拉的反应越灵敏。0表示不跟随手势下拉，1表示等比例跟随手势下拉。 有效值为0-1之间的值，小于0的值会被视为0，大于1的值会被视为1。 | value | - |

### public func pullToRefresh(value: Bool): This

```cangjie
public func pullToRefresh(value: Bool): This
```
功能： 设置当下拉距离超过refreshOffset时是否能触发刷新。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 当下拉距离超过refreshOffset时是否能触发刷新。true表示能触发刷新，false表示不能触发刷新。 初始值：true。 | value | - |

### public func refreshOffset(value: Float64): This

```cangjie
public func refreshOffset(value: Float64): This
```
功能： 设置触发刷新的下拉偏移量，当下拉距离小于该属性设置值时离手不会触发刷新。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float64 | 是 | 下拉偏移量，单位vp。 初始值：96.0。 如果取值为0或负数的时候此接口采用初始值。 | value | - |

### public func refreshOffset(value: Int32): This

```cangjie
public func refreshOffset(value: Int32): This
```
功能： 设置触发刷新的下拉偏移量，当下拉距离小于该属性设置值时离手不会触发刷新。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int32 | 是 | 下拉偏移量，单位vp。 初始值：96。 如果取值为0或负数的时候此接口采用初始值。 | value | - |

### public func onOffsetChange(callback: (Float64) -> Unit): This

```cangjie
public func onOffsetChange(callback: (Float64) -> Unit): This
```
功能： 下拉距离发生变化时触发回调。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (Float64)->Unit | 是 | 下拉距离。 单位：vp。 | callback | - |

### public func onRefreshing(callback: ()-> Unit): This

```cangjie
public func onRefreshing(callback: ()-> Unit): This
```
功能： 进入刷新状态时触发回调。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ()->Unit | 是 | 进入刷新状态时触发回调。 | callback | - |

### public func onStateChange(callback: (RefreshStatus)-> Unit): This

```cangjie
public func onStateChange(callback: (RefreshStatus)-> Unit): This
```
功能： 设置刷新状态变更时，触发回调。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ( RefreshStatus )->Unit | 是 | 刷新状态。 | callback | - |


## Classes

### RefreshParams

class RefreshParams
```cangjie
public class RefreshParams { public var refreshing: Bool public var changeEvent:(Bool) -> Unit public init(refreshing!: Bool) public init(refreshing!: (Bool, (Bool) -> Unit)) }
```
功能： 用于设置Refresh组件参数。

#### var changeEvent

```cangjie
public var changeEvent:(Bool) -> Unit
```
功能： 配合 @Binder 宏使用，用于refreshing属性的双向绑定。
Type: (Bool)->Unit; Readable: true; Writable: true

#### var refreshing

```cangjie
public var refreshing: Bool
```
功能： 当前组件是否正在刷新。
Type: Bool; Readable: true; Writable: true



## Examples

```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*
import std.time.*
import std.sync.*
import ohos.concurrency.*

class MyDataSource <: IDataSource<Int64> {
 public MyDataSource(let data_: ArrayList<Int64>) {}
 public var listenerOp: Option<DataChangeListener> = None
 public func getData(index: Int64): Int64 {
 return data_[index]
 }

 public func onRegisterDataChangeListener(listener: DataChangeListener): Unit {
 listenerOp = listener
 }

 public func onUnregisterDataChangeListener(listener: DataChangeListener): Unit {
 listenerOp = None
 }

 public func totalCount(): Int64 {
 return data_.size
 }
}

@Entry
@Component
class EntryView {
 @State
 var isRefreshing: Bool = false
 let myDataSource: MyDataSource = MyDataSource(ArrayList<Int64>(10, {i => i}))
 @State
 var status: String = "Inactive"
 @State
 var onRefreshStatus: String = "noRefresh"
 @State
 var ratio: Float64 = 1.0
 @State
 var maxRefreshingHeight: Float64 = 100.0

 func build() {
 Column() {
 Text(status)
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 .id("StatusText")
 Text(onRefreshStatus)
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 .id("OnRefreshText")

 Refresh(RefreshParams(refreshing: @Binder(isRefreshing))) {
 Column {
 LazyForEach(
 myDataSource,
 itemGeneratorFunc: {
 element: Int64, index: Int64 => Text(element.toString())
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 }
 )
 }.width(100.percent).backgroundColor(0x89CFF0)
 }
 .width(100.percent)
 .height(100.percent)
 .id("refresh")
 .pullDownRatio(this.ratio)
 .pullToRefresh(true)
 .refreshOffset(16)
 .onRefreshing(
 {
 =>
 onRefreshStatus = "Refresh"
 Timer.once(2000 * Duration.millisecond) {
 => launch {
 this.isRefreshing = false
 onRefreshStatus = "NoRefresh"
 }
 }
 }
 )
 .onStateChange({
 refreshStatus: RefreshStatus =>
 var status = match (refreshStatus) {
 case Inactive => "Inactive"
 case Drag => "Drag"
 case OverDrag => "OverDrag"
 case Refresh => "Refresh"
 case Done => "Done"
 case _ => ""
 }
 })
 .onOffsetChange({offset: Float64 => this.ratio = 0.75})
 .backgroundColor(0x89CFF0)
 }

 }
}
```
```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*
import std.time.*
import std.sync.*
import ohos.concurrency.*
import std.math.*

class MyDataSource <: IDataSource<Int64> {
 public MyDataSource(let data_: ArrayList<Int64>) {}
 public var listenerOp: Option<DataChangeListener> = None
 public func getData(index: Int64): Int64 {
 return data_[index]
 }

 public func onRegisterDataChangeListener(listener: DataChangeListener): Unit {
 listenerOp = listener
 }

 public func onUnregisterDataChangeListener(listener: DataChangeListener): Unit {
 listenerOp = None
 }

 public func totalCount(): Int64 {
 return data_.size
 }
}

@Entry
@Component
class EntryView {
 @State
 var isRefreshing: Bool = false
 let myDataSource: MyDataSource = MyDataSource(ArrayList<Int64>(10, {i => i}))
 @State
 var status: String = "Inactive"
 @State
 var onRefreshStatus: String = "noRefresh"
 @State
 var ratio: Float64 = 1.0
 @State
 var maxRefreshingHeight: Float64 = 100.0

 func build() {
 Column() {
 Text(status)
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 .id("StatusText")
 Text(onRefreshStatus)
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 .id("OnRefreshText")
 Refresh(RefreshParams(refreshing: @Binder(isRefreshing))) {
 Column {
 LazyForEach(
 myDataSource,
 itemGeneratorFunc: {
 element: Int64, index: Int64 => Text(element.toString())
 .size(width: 50.percent, height: 50.vp)
 .borderWidth(1)
 .borderColor(Color.BLACK)
 .backgroundColor(0xFFFFFF)
 .borderRadius(15)
 .textAlign(TextAlign.Center)
 .fontSize(30)
 .margin(top: 20.vp)
 }
 )
 }.width(100.percent).backgroundColor(0x89CFF0)
 }
 .width(100.percent)
 .height(100.percent)
 .id("refresh")
 .pullDownRatio(this.ratio)
 .pullToRefresh(true)
 .refreshOffset(100)
 .onRefreshing(
 {
 =>
 onRefreshStatus = "Refresh"

 Timer.once(2000 * Duration.millisecond) {
 => launch {
 this.isRefreshing = false
 onRefreshStatus = "NoRefresh"
 }
 }
 }
 )
 .onStateChange({
 refreshStatus: RefreshStatus => status = match (refreshStatus) {
 case Inactive => "Inactive"
 case Drag => "Drag"
 case OverDrag => "OverDrag"
 case Refresh => "Refresh"
 case Done => "Done"
 case _ => ""
 }
 })// 通过该事件调整pullDownRatio，控制最大下拉距离
 .onOffsetChange({offset: Float64 => this.ratio = 1.0 - pow((offset / this.maxRefreshingHeight), 3)})
 .backgroundColor(0x89CFF0)
 }
 }
}
```

