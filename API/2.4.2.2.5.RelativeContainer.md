# RelativeContainer

相对布局组件，用于复杂场景中元素对齐的布局。

## Component Constructors

### public init()

```cangjie
public init()
```
功能： 创建一个RelativeContainer组件。

### public init(child: () -> Unit)

```cangjie
public init(child: () -> Unit)
```
功能： 创建一个RelativeContainer组件。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ()->Unit | 是 | 声明容器子组件。 | child | - |


## Functions

### public func barrier(value: Array<BarrierStyle>): This

```cangjie
public func barrier(value: Array<BarrierStyle>): This
```
功能： 设置RelativeContainer容器内的屏障，Array中每个项目即为一条barrier。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Array< BarrierStyle > | 是 | RelativeContainer容器内的屏障。 | value | - |

### public func barrier(value: Array<LocalizedBarrierStyle>): This

```cangjie
public func barrier(value: Array<LocalizedBarrierStyle>): This
```
功能： 设置RelativeContainer容器内的屏障，Array中每个项目即为一条barrier。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Array< LocalizedBarrierStyle > | 是 | RelativeContainer容器内的屏障。 | value | - |

### public func guideLine(value: Array<GuideLineStyle>): This

```cangjie
public func guideLine(value: Array<GuideLineStyle>): This
```
功能： 设置RelativeContainer容器内的辅助线，Array中每个项目即为一条guideline。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Array< GuideLineStyle > | 是 | RelativeContainer容器内的辅助线。 | value | - |


## Classes

### BarrierStyle

class BarrierStyle
```cangjie
public class BarrierStyle { public BarrierStyle( public var id: String, public var direction: BarrierDirection, public var referencedId: Array<String> ) }
```
功能： barrier参数，用于定义一条barrier的id、方向和生成时所依赖的组件。

#### var direction

```cangjie
public var direction: BarrierDirection
```
功能： 指定barrier的方向。垂直方向（TOP，BOTTOM）的barrier仅能作为组件的水平方向锚点，用作垂直方向锚点时值为0；水平方向（LEFT，RIGHT）的barrier仅能作为组件的垂直方向锚点，用作水平方向锚点时值为0。
Type: BarrierDirection; Readable: true; Writable: true

#### var id

```cangjie
public var id: String
```
功能： barrier的id，必须是唯一的并且不可与容器内组件重名。
Type: String; Readable: true; Writable: true

#### var referencedId

```cangjie
public var referencedId: Array<String>
```
功能： 指定生成barrier所依赖的组件。
Type: Array<String>; Readable: true; Writable: true


### GuideLinePosition

class GuideLinePosition
```cangjie
public class GuideLinePosition { public GuideLinePosition( public var start!: ?Length = None, public var end!: ?Length = None ) }
```
功能： guideLine位置参数，用于定义guideline的位置。

#### var end

```cangjie
public var end:?Length = None
```
功能： guideline距离容器右侧或者底部的距离。
Type: ? Length; Readable: true; Writable: true

#### var start

```cangjie
public var start:?Length = None
```
功能： guideline距离容器左侧或者顶部的距离。
Type: ? Length; Readable: true; Writable: true


### GuideLineStyle

class GuideLineStyle
```cangjie
public class GuideLineStyle { public GuideLineStyle ( public var id: String, public var direction: Axis, public var position: GuideLinePosition ) }
```
功能： guideLine参数，用于定义一条guideline的id、方向和位置。

#### var direction

```cangjie
public var direction: Axis
```
功能： 指定guideline的方向。垂直方向的guideline仅能作为组件水平方向的锚点，作为垂直方向的锚点时值为0；水平方向的guideline仅能作为组件垂直方向的锚点，作为水平方向的锚点时值为0。
Type: Axis; Readable: true; Writable: true

#### var id

```cangjie
public var id: String
```
功能： guideline的id，必须是唯一的并且不可与容器内组件重名。
Type: String; Readable: true; Writable: true

#### var position

```cangjie
public var position: GuideLinePosition
```
功能： 指定guideline的位置。当未声明或声明异常值（如undefined）时，guideline的位置默认为start: 0。start和 end两种声明方式选择一种即可。若同时声明，仅start生效。若容器在某个方向的size被声明为"auto"，则该方向上guideline的位置只能使用start方式声明（不允许使用百分比）。
Type: GuideLinePosition; Readable: true; Writable: true


### LocalizedBarrierStyle

class LocalizedBarrierStyle
```cangjie
public class LocalizedBarrierStyle { public LocalizedBarrierStyle( public var id: String, public var localizedDirection: LocalizedBarrierDirection, public var referencedId: Array<String> ) }
```
功能： barrier参数，用于定义一条barrier的id、方向和生成时所依赖的组件。

#### var id

```cangjie
public var id: String
```
功能： barrier的id，必须是唯一的并且不可与容器内组件重名。
Type: String; Readable: true; Writable: true

#### var localizedDirection

```cangjie
public var localizedDirection: LocalizedBarrierDirection
```
功能： 指定barrier的方向。
Type: LocalizedBarrierDirection; Readable: true; Writable: true

#### var referencedId

```cangjie
public var referencedId: Array<String>
```
功能： 指定生成barrier所依赖的组件。
Type: Array<String>; Readable: true; Writable: true



## Examples

```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100)
 .backgroundColor(0xff3333)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("__container__", VerticalAlign.Top),
 left: HorizontalAnchor("__container__", HorizontalAlign.Start)
 )
 ).id("row1")
 Row().width(100).height(100)
 .backgroundColor(0xFFCC00)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("__container__", VerticalAlign.Top),
 right: HorizontalAnchor("__container__", HorizontalAlign.End)
 )
 ).id("row2")
 Row().height(100)
 .backgroundColor(0xFF6633)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Bottom),
 left: HorizontalAnchor("row1", HorizontalAlign.End),
 right: HorizontalAnchor("row2", HorizontalAlign.Start)
 )
 ).id("row3")
 Row()
 .backgroundColor(0xFF9966)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row3", VerticalAlign.Bottom),
 bottom: VerticalAnchor("__container__", VerticalAlign.Bottom),
 left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("row1", HorizontalAlign.End)
 )
 ).id("row4")
 Row()
 .backgroundColor(0xff3333)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row3", VerticalAlign.Bottom),
 bottom: VerticalAnchor("__container__", VerticalAlign.Bottom),
 left: HorizontalAnchor("row2", HorizontalAlign.Start),
 right: HorizontalAnchor("__container__", HorizontalAlign.End)
 )
 ).id("row5")
 }
 .width(300).height(300)
 .margin(left: 50.vp)
 .border(width: 2.vp, color: Color(0x6699ff))
 }.height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100)
 .backgroundColor(0xff3333)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("__container__", VerticalAlign.Top),
 left: HorizontalAnchor("__container__", HorizontalAlign.Start)
 )
 ).id("row1")
 .margin(10)
 Row().width(100).height(100)
 .backgroundColor(0xFFCC00)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Top),
 left: HorizontalAnchor("row1", HorizontalAlign.End)
 )
 ).id("row2")
 Row().height(100).width(100)
 .backgroundColor(0xFF6633)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Bottom),
 left: HorizontalAnchor("row1", HorizontalAlign.Start)
 )
 ).id("row3")
 Row().width(100).height(100)
 .backgroundColor(0xFF9966)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row2", VerticalAlign.Bottom),
 left: HorizontalAnchor("row3", HorizontalAlign.End),
 )
 ).id("row4")
 .margin(10)
 }
 .width(300).height(300)
 .margin(left: 50.vp)
 .border(width: 2.vp, color: Color(0x6699ff))
 }.height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100).backgroundColor(0xff3333).alignRules(
 AlignRuleOption(
 top: VerticalAnchor("__container__", VerticalAlign.Top),
 bottom: VerticalAnchor("__container__", VerticalAlign.Bottom),
 left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("__container__", HorizontalAlign.End),
 bias: Bias(vertical: 0.3)
 )
 ).id("row1")
 }
 .width(300).height(300)
 .margin(left: 50.vp)
 .border(width: 2.vp, color: Color(0x6699ff))
 }.height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100).backgroundColor(0xff3333).alignRules(
 AlignRuleOption(
 top: VerticalAnchor("guideline2", VerticalAlign.Top),
 left: HorizontalAnchor("guideline1", HorizontalAlign.End),
 )
 ).id("row1")
 }.width(300).height(300).margin(left: 50.vp).border(width: 2.vp, color: Color(0x6699ff))
 .guideLine(
 [GuideLineStyle("guideline1", Axis.Vertical, GuideLinePosition(start: 50.vp)),
 GuideLineStyle("guideline2", Axis.Horizontal, GuideLinePosition(start: 50.vp))])
 }.height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100)
 .backgroundColor(0xff3333)
 .id("row1")

 Row().width(100).height(100)
 .backgroundColor(0xFFCC00)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Bottom),
 middle: HorizontalAnchor("row1", HorizontalAlign.End)
 )
 ).id("row2")

 Row().height(100).width(100)
 .backgroundColor(0xFF6633)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Top),
 left: HorizontalAnchor("barrier1", HorizontalAlign.End)
 )
 ).id("row3")

 Row().width(50).height(50)
 .backgroundColor(0xFF9966)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("barrier2", VerticalAlign.Bottom),
 left: HorizontalAnchor("row1", HorizontalAlign.Start),
 )
 ).id("row4")
 }.width(300).height(300)
 .margin(left: 50.vp)
 .border(width: 2.vp, color: Color(0x6699ff))
 .barrier(
 [BarrierStyle("barrier1", BarrierDirection.RIGHT, ["row1", "row2"]),
 BarrierStyle("barrier2", BarrierDirection.BOTTOM, ["row1", "row2"])])
 }.height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import ohos.state_macro_manage.*

@Entry
@Component
class EntryView{
 func build(){
 Row(){
 RelativeContainer(){
 Row(){
 Text('row1')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0xa3cf62)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("row2", HorizontalAlign.Start),
 top: VerticalAnchor("__container__", VerticalAlign.Top)
 )
 )
 .id("row1")
 .chainMode(Axis.Horizontal, ChainStyle.SPREAD)

 Row(){
 Text('row2')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x00ae9d)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row1", HorizontalAlign.End),
 right: HorizontalAnchor("row3", HorizontalAlign.Start),
 top: VerticalAnchor("row1", VerticalAlign.Top)
 )
 )
 .id("row2")

 Row(){
 Text('row3')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x0a59f7)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row2", HorizontalAlign.End),
 right: HorizontalAnchor("__container__", HorizontalAlign.End),
 top: VerticalAnchor("row1", VerticalAlign.Top)
 )
 )
 .id("row3")

 Row(){
 Text('row4')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0xa3cf62)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("row5", HorizontalAlign.Start),
 center: VerticalAnchor("__container__", VerticalAlign.Center)
 )
 )
 .id("row4")
 .chainMode(Axis.Horizontal, ChainStyle.SPREAD_INSIDE)

 Row(){
 Text('row5')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x00ae9d)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row4", HorizontalAlign.End),
 right: HorizontalAnchor("row6", HorizontalAlign.Start),
 top: VerticalAnchor("row4", VerticalAlign.Top)
 )
 )
 .id("row5")

 Row(){
 Text('row6')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x0a59f7)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row5", HorizontalAlign.End),
 right: HorizontalAnchor("__container__", HorizontalAlign.End),
 top: VerticalAnchor("row4", VerticalAlign.Top)
 )
 )
 .id("row6")

 Row(){
 Text('row7')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0xa3cf62)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("row8", HorizontalAlign.Start),
 bottom: VerticalAnchor("__container__", VerticalAlign.Bottom)
 )
 )
 .id("row7")
 .chainMode(Axis.Horizontal, ChainStyle.PACKED)

 Row(){
 Text('row8')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x00ae9d)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row7", HorizontalAlign.End),
 right: HorizontalAnchor("row9", HorizontalAlign.Start),
 top: VerticalAnchor("row7", VerticalAlign.Top)
 )
 )
 .id("row8")

 Row(){
 Text('row9')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x0a59f7)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row8", HorizontalAlign.End),
 right: HorizontalAnchor("__container__", HorizontalAlign.End),
 top: VerticalAnchor("row7", VerticalAlign.Top)
 )
 )
 .id("row9")
 }
 .width(300).height(300)
 .margin(left: 50)
 .border(width: 2, color: 0x6699FF)
 }
 .height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry

import kit.UIKit.*
import ohos.state_macro_manage.*

@Entry
@Component
class EntryView{

 func build() {
 Row(){
 RelativeContainer(){
 Row(){
 Text('row1')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0xa3cf62)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("__container__", HorizontalAlign.Start),
 right: HorizontalAnchor("row2", HorizontalAlign.Start),
 center: VerticalAnchor("__container__", VerticalAlign.Center),
 bias: Bias(horizontal: 0.0)
 )
 )
 .id("row1")
 .chainMode(Axis.Horizontal, ChainStyle.PACKED)

 Row(){
 Text('row2')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x00ae9d)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row1", HorizontalAlign.End),
 right: HorizontalAnchor("row3", HorizontalAlign.Start),
 top: VerticalAnchor("row1", VerticalAlign.Top),
 )
 )
 .id("row2")

 Row(){
 Text('row3')
 }
 .justifyContent(FlexAlign.Center)
 .width(80)
 .height(80)
 .backgroundColor(0x0a59f7)
 .alignRules(AlignRuleOption(left: HorizontalAnchor("row2", HorizontalAlign.End),
 right: HorizontalAnchor("__container__", HorizontalAlign.End),
 top: VerticalAnchor("row1", VerticalAlign.Top),
 )
 )
 .id("row3")
 }
 .width(300).height(300)
 .margin(left: 50)
 .border(width: 2, color: 0x6699FF)
 }
 .height(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
import std.collection.*

@Entry
@Component
class EntryView {
 func build() {
 Row() {
 RelativeContainer() {
 Row().width(100).height(100)
 .backgroundColor(0xff3333)
 .id("row1")

 Row().width(100).height(100)
 .backgroundColor(0xFFCC00)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Bottom),
 middle: HorizontalAnchor("row1", HorizontalAlign.End)
 )
 ).id("row2")

 Row().height(100).width(100)
 .backgroundColor(0xFF6633)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("row1", VerticalAlign.Top),
 left: HorizontalAnchor("barrier1", HorizontalAlign.End)
 )
 ).id("row3")

 Row().width(50).height(50)
 .backgroundColor(0xFF9966)
 .alignRules(
 AlignRuleOption(
 top: VerticalAnchor("barrier2", VerticalAlign.Bottom),
 left: HorizontalAnchor("row1", HorizontalAlign.Start),
 )
 ).id("row4")
 }.width(300).height(300)
 .margin(left: 50.vp)
 .border(width: 2.vp, color: Color(0x6699ff))
 .direction(Direction.Rtl)
 .barrier(
 [LocalizedBarrierStyle("barrier1", LocalizedBarrierDirection.END, ["row1", "row2"]),
 LocalizedBarrierStyle("barrier2", LocalizedBarrierDirection.BOTTOM, ["row1", "row2"])])
 }.height(100.percent)
 }
}
```

