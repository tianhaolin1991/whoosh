# Slider

滑动条组件，通常用于用快速调节设置值，如音量调节、亮度调节等应用场景。

## Component Constructors

### public init( min!: Float64 = 0.0, max!: Float64 = 100.0, step!: Float64 = 1.0, value!: Float64 = min, style!: SliderStyle = SliderStyle.OutSet, direction!: Axis = Axis.Horizontal, reverse!: Bool = false )

```cangjie
public init( min!: Float64 = 0.0, max!: Float64 = 100.0, step!: Float64 = 1.0, value!: Float64 = min, style!: SliderStyle = SliderStyle.OutSet, direction!: Axis = Axis.Horizontal, reverse!: Bool = false )
```
功能： 创建一个滑动条组件。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float64 | 否 | 命名参数。 设置最小值。 | min | 0.0 |
| Float64 | 否 | 命名参数。 设置最大值。 初始值：100。 说明： min >= max异常情况，min取初始值0，max取初始值100。 value不在[min, max]范围之内，取min或者max，靠近min取min，靠近max取max。 | max | 100.0 |
| Float64 | 否 | 命名参数。 设置滑动条滑动步长。 说明： 当step<=0，或step>=max-min时，取初始值。 | step | 1.0 |
| Float64 | 否 | 命名参数。 当前进度值。 | value | min |
| SliderStyle | 否 | 命名参数。 设置滑动条的滑块样式。 | style | SliderStyle.OutSet |
| Axis | 否 | 命名参数。 设置滑动条滑动方向为水平或竖直方向。 | direction | Axis.Horizontal |
| Bool | 否 | 命名参数。 设置滑动条取值范围是否反向。 说明： 设置为false时，水平方向滑动条为从左向右滑动，竖直方向滑动条从上向下滑动。 设置为true时，水平方向滑动条为从右向左滑动，竖直方向滑动条从下向上滑动。 | reverse | false |


## Functions

### public func blockBorderColor(value: ResourceColor): This

```cangjie
public func blockBorderColor(value: ResourceColor): This
```
功能： 设置滑块描边颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 滑块描边颜色。 初始值：0x00000000。 | value | - |

### public func blockBorderWidth(value: Length): This

```cangjie
public func blockBorderWidth(value: Length): This
```
功能： 设置滑块描边粗细。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 滑块描边粗细。不支持百分比设置。 | value | - |

### public func blockColor(value: ResourceColor): This

```cangjie
public func blockColor(value: ResourceColor): This
```
功能： 设置滑块的颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 滑块的颜色。 初始值：@r(sys.color.ohos_id_color_foreground_contrary)。 | value | - |

### public func blockSize(width!: Length = 0.vp, height!: Length = 0.vp): This

```cangjie
public func blockSize(width!: Length = 0.vp, height!: Length = 0.vp): This
```
功能： 设置滑块大小。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 否 | 命名参数。 滑块宽度。 | width | 0.vp |
| Length | 否 | 命名参数。 滑块高度。 | height | 0.vp |

### public func blockStyle(`type`: SliderBlockType, image!: String = "", shape!: ?ShapeAbstract = None): This

```cangjie
public func blockStyle(`type`: SliderBlockType, image!: String = "", shape!: ?ShapeAbstract = None): This
```
功能： 设置滑块形状参数。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| SliderBlockType | 是 | 滑块形状参数。 初始值： SliderBlockType.DEFAULT，使用圆形滑块。 | type | - |
| String | 否 | 命名参数。 设置滑块图片资源。图片显示区域大小由blockSize属性控制，请勿输入尺寸过大的图片。 | image | "" |
| ? ShapeAbstract | 否 | 命名参数。 设置滑块使用的自定义形状。包含 Circle 、 Ellipse 、 Path 、 Rect 。 | shape | None |

### public func blockStyle(`type`: SliderBlockType, image!: AppResource, shape!: ?ShapeAbstract = None): This

```cangjie
public func blockStyle(`type`: SliderBlockType, image!: AppResource, shape!: ?ShapeAbstract = None): This
```
功能： 设置滑块形状参数。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| SliderBlockType | 是 | 滑块形状参数。 初始值： SliderBlockType.DEFAULT，使用圆形滑块。 | type | - |
| AppResource | 是 | 命名参数。 设置滑块图片资源。图片显示区域大小由blockSize属性控制，请勿输入尺寸过大的图片。 | image | - |
| ? ShapeAbstract | 否 | 命名参数。 设置滑块使用的自定义形状。包含 Circle 、 Ellipse 、 Path 、 Rect 。 | shape | None |

### public func maxLabel(value: Float64): This

```cangjie
public func maxLabel(value: Float64): This
```
功能： 根据指定的浮点值设置可滑动的最大值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float64 | 是 | 最大值。 | value | - |

### public func minLabel(value: Float64): This

```cangjie
public func minLabel(value: Float64): This
```
功能： 根据指定的浮点值设置可滑动的最小值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float64 | 是 | 最小值。 | value | - |

### public func minResponsiveDistance(value: Float32): This

```cangjie
public func minResponsiveDistance(value: Float32): This
```
功能： 设置滑动响应的最小距离。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Float32 | 是 | 设置滑动响应的最小距离，滑动超过此距离后才响应使滑块滑动。 说明 ： 单位与参数min和max一致。 当value小于0、大于MAX-MIN或非法值时，取初始值。 初始值：0。 | value | - |

### public func minResponsiveDistance(value: Int64): This

```cangjie
public func minResponsiveDistance(value: Int64): This
```
功能： 设置滑动响应的最小距离。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Int64 | 是 | 设置滑动响应的最小距离，滑动超过此距离后才响应使滑块滑动。 说明 ： 单位与参数min和max一致。 当value小于0、大于MAX-MIN或非法值时，取初始值。 初始值：0。 | value | - |

### public func selectedBorderRadius(value: Length): This

```cangjie
public func selectedBorderRadius(value: Length): This
```
功能： 设置已滑动部分（高亮）圆角半径。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 已选择部分圆角半径。 初始值：style值为SliderStyle.InSet或SliderStyle.OutSet时，跟随底板圆角；style值为SliderStyle.NONE时，为0。不支持百分比设置，设定值小于0时取初始值。 | value | - |

### public func selectedColor(value: ResourceColor): This

```cangjie
public func selectedColor(value: ResourceColor): This
```
功能： 根据指定的Color设置滑轨已滑动部分的颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 滑轨已滑动部分的颜色。 初始值：@r(sys.color.ohos_id_color_emphasize)。 | value | - |

### public func showSteps(value: Bool): This

```cangjie
public func showSteps(value: Bool): This
```
功能： 设置当前是否显示步长刻度值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 当前是否显示步长刻度值。 初始值：false。 | value | - |

### public func showTips(value: Bool, content!: ?String = None): This

```cangjie
public func showTips(value: Bool, content!: ?String = None): This
```
功能： 设置滑动时是否显示气泡提示。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Bool | 是 | 滑动时是否显示气泡提示。 初始值：false。 | value | - |
| ?String | 否 | 命名参数。 气泡提示的文本内容，默认显示当前百分比。 | content | None |

### public func slideRange( from!: ?Float32 = None, to!: ?Float32 = None): This

```cangjie
public func slideRange( from!: ?Float32 = None, to!: ?Float32 = None): This
```
功能： 设置有效滑动区间。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ?Float32 | 否 | 命名参数。 设置有效滑动区间的开始。 | from | None |
| ?Float32 | 否 | 命名参数。 设置有效滑动区间的结束。 | to | None |

### public func slideRange(from!: ?Int64 = None, to!: ?Int64 = None): This

```cangjie
public func slideRange(from!: ?Int64 = None, to!: ?Int64 = None): This
```
功能： 设置有效滑动区间。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ?Int64 | 否 | 命名参数。 设置有效滑动区间的开始。 | from | None |
| ?Int64 | 否 | 命名参数。 设置有效滑动区间的结束。 | to | None |

### public func sliderInteractionMode(value: SliderInteraction): This

```cangjie
public func sliderInteractionMode(value: SliderInteraction): This
```
功能： 设置用户与滑动条组件交互方式。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| SliderInteraction | 是 | 用户与滑动条组件交互方式。 初始值：SliderInteraction.SLIDE_AND_CLICK。 | value | - |

### public func stepColor(value: ResourceColor): This

```cangjie
public func stepColor(value: ResourceColor): This
```
功能： 设置刻度颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 刻度颜色。 初始值： @r(sys.color.ohos_id_color_foreground)混合 @r(sys.color.ohos_id_alpha_normal_bg)透明度的颜色。 | value | - |

### public func stepSize(value: Length): This

```cangjie
public func stepSize(value: Length): This
```
功能： 设置刻度大小（直径）。当值为0时，刻度点不显示，当值小于0时，取初始值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 刻度大小（直径）。 初始值：4.vp。 取值范围：[0, trackThickness)。 | value | - |

### public func trackBorderRadius(value: Length): This

```cangjie
public func trackBorderRadius(value: Length): This
```
功能： 设置底板圆角半径。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 底板圆角半径。 初始值：style值为SliderStyle.OutSet初始值为2.vp。 style值为SliderStyle.InSet初始值为10.vp。不支持百分比设置。设定值小于0时取初始值。 | value | - |

### public func trackColor(value: ResourceColor): This

```cangjie
public func trackColor(value: ResourceColor): This
```
功能： 根据指定的Color设置滑轨的背景颜色。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ResourceColor | 是 | 滑轨的背景颜色。 说明： 设置渐变色时，若颜色断点颜色值为非法值或者渐变色断点为空时，渐变色不起效果。 初始值：@r(sys.color.ohos_id_color_component_normal)。 | value | - |

### public func trackThickness(value: Length): This

```cangjie
public func trackThickness(value: Length): This
```
功能： 根据指定的Length设置滑轨的粗细。设置为小于等于0的值时，取初始值。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| Length | 是 | 滑轨的粗细。 初始值：当参数style的值设置SliderStyle.OutSet 时为 4.0.vp，SliderStyle.InSet时为20.0.vp。 | value | - |

### public func onChange(callback: (Float64, SliderChangeMode)->Unit): This

```cangjie
public func onChange(callback: (Float64, SliderChangeMode)->Unit): This
```
功能： Slider拖动或点击时触发事件回调。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| (Float64, SliderChangeMode )->Unit | 是 | Slider拖动或点击时触发事件回调。 参数一：当前滑动进度值，变化范围为对应步长steps数组。 参数二：事件触发的相关状态值。 | callback | - |

### public func onChange(callback: ()->Unit): This

```cangjie
public func onChange(callback: ()->Unit): This
```
功能： Slider拖动或点击时触发事件回调。

**Parameters:**

| type | required | description | name | default |
| --- | --- | --- | --- | --- |
| ()->Unit | 是 | 事件触发的相关状态值。 | callback | - |


## Enums

### SliderBlockType

enum SliderBlockType
```cangjie
public enum SliderBlockType { | DEFAULT | IMAGE | SHAPE }
```
功能： Slider组件滑块形状枚举。

#### DEFAULT

```cangjie
DEFAULT
```
功能： 使用默认滑块（圆形）。

#### IMAGE

```cangjie
IMAGE
```
功能： 使用图片资源作为滑块。

#### SHAPE

```cangjie
SHAPE
```
功能： 使用自定义形状作为滑块。


### SliderInteraction

enum SliderInteraction
```cangjie
public enum SliderInteraction { | SLIDE_AND_CLICK | SLIDE_ONLY | SLIDE_AND_CLICK_UP }
```
功能： 用户与滑动条组件交互方式枚举。

#### SLIDE_AND_CLICK

```cangjie
SLIDE_AND_CLICK
```
功能： 用户可拖拽滑块或者点击滑轨使滑块移动，鼠标或手指按下即发生移动。

#### SLIDE_AND_CLICK_UP

```cangjie
SLIDE_AND_CLICK_UP
```
功能： 用户可拖拽滑块或者点击滑轨使滑块移动，鼠标或手指抬起时，若与屏幕按压位置一致，则触发移动。

#### SLIDE_ONLY

```cangjie
SLIDE_ONLY
```
功能： 不允许用户通过点击滑轨使滑块移动。


### SliderStyle

enum SliderStyle
```cangjie
public enum SliderStyle { | OutSet | InSet | NONE }
```
功能： Slider的滑块与滑轨显示样式。

#### OutSet

```cangjie
OutSet
```
功能： 滑块在滑轨内。

#### InSet

```cangjie
InSet
```
功能： 旋钮在内样式。

#### NONE

```cangjie
NONE
```
功能： 无滑块。


### SliderChangeMode

enum SliderChangeMode
```cangjie
public enum SliderChangeMode { | Begin | Moving | End | Click }
```
功能： Slider拖动或点击时触发事件的状态值。

#### Begin

```cangjie
Begin
```
功能： 手势/鼠标接触或者按下滑块。

#### Moving

```cangjie
Moving
```
功能： 正在拖动滑块过程中。

#### End

```cangjie
End
```
功能： 手势/鼠标离开滑块。

#### Click

```cangjie
Click
```
功能： 点击滑动条使滑块位置移动。



## Examples

```cangjie
package ohos_app_cangjie_entry
internal import ohos.base.*
import kit.UIKit.*
import ohos.state_macro_manage.*
internal import ohos.state_manage.SubscriberManager
internal import ohos.state_manage.ObservedProperty
internal import ohos.state_manage.LocalStorage
internal import ohos.router.Router

@Entry
@Component
class EntryView {
 @State var outSetValueOne: Float64 = 40.00
 @State var inSetValueOne: Float64 = 40.00
 @State var noneValueOne: Float64 = 40.00
 @State var outSetValueTwo: Float64 = 40.00
 @State var inSetValueTwo: Float64 = 40.00
 @State var vOutSetValueOne: Float64 = 40.00
 @State var vInSetValueOne: Float64 = 40.00
 @State var vOutSetValueTwo: Float64 = 40.00
 @State var vInSetValueTwo: Float64 = 40.00

 func build() {
 Column {
 Text('outset slider').fontSize(9).fontColor(0xCCCCCC).width(90.percent).margin(15)
 Row {
 Slider(
 value: this.outSetValueOne,
 min: 0.0,
 max: 100.0,
 style: SliderStyle.OutSet
 ).showTips(true).onChange({
 value: Float64, mode: SliderChangeMode => this.outSetValueOne = value
 })
 Text("${Int64(this.outSetValueOne)}").fontSize(12)
 }.width(80.percent)
 Row() {
 Slider(
 value: this.outSetValueTwo,
 step: 10.0,
 style: SliderStyle.OutSet
 ).showSteps(true).onChange({
 value: Float64, mode: SliderChangeMode => this.outSetValueTwo = value
 })
 Text("${Int64(this.outSetValueTwo)}").fontSize(12)
 }.width(80.percent)

 Text('inset slider').fontSize(9).fontColor(0xCCCCCC).width(90.percent).margin(15)
 Row() {
 Slider(
 value: this.inSetValueOne,
 min: 0.0,
 max: 100.0,
 style: SliderStyle.InSet
 )
 .blockColor(0x191970)
 .trackColor(0xADD8E6)
 .selectedColor(0x4169E1)
 .showTips(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.inSetValueOne = value
 })
 Text("${Int64(this.inSetValueOne)}").fontSize(12)
 }.width(80.percent)
 Row() {
 Slider(
 value: this.inSetValueTwo,
 step: 10.0,
 style: SliderStyle.InSet
 )
 .blockColor(0x191970)
 .trackColor(0xADD8E6)
 .selectedColor(0x4169E1)
 .showSteps(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.inSetValueTwo = value
 })
 Text("${Int64(this.inSetValueTwo)}").fontSize(12)
 }.width(80.percent)

 Text('none slider').fontSize(9).fontColor(0xCCCCCC).width(90.percent).margin(15)
 Row() {
 Slider(
 value: this.noneValueOne,
 min: 0.0,
 max: 100.0,
 style: SliderStyle.NONE
 )
 .blockColor(0x191970)
 .trackColor(0xADD8E6)
 .selectedColor(0x4169E1)
 .showTips(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.noneValueOne = value
 })
 Text("${Int64(this.noneValueOne)}").fontSize(12)
 }.width(80.percent)

 Row() {
 Column() {
 Text('vertical outset slider').fontSize(9).fontColor(0xCCCCCC).width(50.percent).margin(15)
 Row() {
 Text("").width(10.percent)
 Slider(
 value: this.vOutSetValueOne,
 style: SliderStyle.OutSet,
 direction: Axis.Vertical
 )
 .blockColor(0x191970)
 .trackColor(0xADD8E6)
 .selectedColor(0x4169E1)
 .showTips(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.vOutSetValueOne = value
 })
 Slider(
 value: this.vOutSetValueTwo,
 step: 10.0,
 style: SliderStyle.OutSet,
 direction: Axis.Vertical
 )
 .blockColor(0x191970)
 .trackColor(0xADD8E6)
 .selectedColor(0x4169E1)
 .showSteps(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.vOutSetValueTwo = value
 })
 }
 }.width(50.percent).height(300)

 Column() {
 Text('vertical inset slider').fontSize(9).fontColor(0xCCCCCC).width(50.percent).margin(15)
 Row() {
 Slider(
 value: this.vInSetValueOne,
 style: SliderStyle.InSet,
 direction: Axis.Vertical,
 reverse: true // 竖向的Slider默认是上端是min值，下端是max值，因此想要从下往上滑动，需要设置reverse为true
 )
 .showTips(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.vInSetValueOne = value
 })
 Slider(
 value: this.vInSetValueTwo,
 step: 10.0,
 style: SliderStyle.InSet,
 direction: Axis.Vertical,
 reverse: true
 )
 .showSteps(true)
 .onChange({
 value: Float64, mode: SliderChangeMode => this.vInSetValueTwo = value
 })
 }
 }.width(50.percent).height(300)
 }
 }.width(100.percent)
 }
}
```
```cangjie
package ohos_app_cangjie_entry
import kit.UIKit.*
import ohos.state_macro_manage.*
internal import kit.LocalizationKit.{AppResource, __GenerateResource__}

@Entry
@Component
class EntryView {
 @State var tipsValue: Float64 = 40.0

 func build() {
 Column {
 Text('block').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.OutSet, value: 40.0)
 .blockSize(width: 40.0, height: 40.0)
 .blockBorderColor(Color.RED)
 .blockBorderWidth(5)
 Divider()
 Text('step').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.InSet, value: 40.0, step: 10.0).showSteps(true).stepSize(8).stepColor(0xFFFF00)
 Divider()
 Text('track').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.InSet, value: 40.0).trackBorderRadius(2)
 Divider()
 Text('selected').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.InSet, value: 40.0).selectedBorderRadius(2)
 Divider()
 Text('blockStyle').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.OutSet, value: 40.0).blockStyle(SliderBlockType.DEFAULT)
 Slider(style: SliderStyle.OutSet, value: 40.0).blockStyle(SliderBlockType.IMAGE,
 image: @r(app.media.startIcon))
 // .blockStyle( SliderBlockType.IMAGE, image: "app.media.startIcon" )
 Slider(style: SliderStyle.OutSet, value: 40.0)
 .blockSize(width: 60.px, height: 60.px)
 .blockColor(Color.RED)
 .blockStyle(SliderBlockType.SHAPE, shape: PathShape(commands: 'M60 60 M30 30 L15 56 L45 56 Z'))
 Divider()
 Text('tips').fontSize(9).fontColor(0xCCCCCC).margin(15).width(90.percent)
 Slider(style: SliderStyle.InSet, value: this.tipsValue)
 .showTips(true, content: "${Int64(this.tipsValue)}")
 .onChange({
 value: Float64, mode: SliderChangeMode => this.tipsValue = value
 })
 }
 }
}
```

